<类型标识符> ::= int | float | array'['<无符号整数>']' | array'['<无符号整数>']''['<无符号整数>']'

<字母> ::= _ | a | ... | z | A | ... | Z

<数字> ::= 0 | 1 | ... | 9

<无符号整数> ::= <数字> {<数字>}

<整数> ::= [ + | - ] <无符号整数>

<小数> ::= [ + | - ] <无符号小数>

<无符号小数> ::= <无符号整数>'.'<无符号整数>

<加法运算符> ::= + | -

<乘法运算符> ::= * | /

<关系运算符> ::= < | <= | > | >= | != | ==

<标识符> ::= <字母> {<字母>|<数字>}



<表达式> ::= [ + | - ]<项>{<加法运算符><项>}

<项> ::= <因子>{<乘法运算符><因子>}

<因子> ::= <标识符> | <标识符>'['<表达式>']' | <标识符>'['<表达式']''['<表达式']' | '('<表达式>')' | <整数> | <小数> | <有返回值函数调用语句>



<常量> ::= <整数> | <小数>

<输入输出说明> ::= <输入输出定义>; {<输入输出定义>;}

<输入输出定义> ::= in <类型标识符> <标识符> | out <类型标识符> <标识符>

<变量说明> ::= <变量定义>; {<变量定义>;}

<变量定义> ::= <变量定义无初始化> | <变量定义及初始化>

<变量定义无初始化> ::= <类型标识符> <标识符>{, <标识符>}

<变量定义及初始化> ::= <类型标识符> <标识符> = <常量> | <类型标识符> <标识符> = '{' <常量> {, <常量>} '}' | <类型标识符> <标识符> = '{''{'<常量>{, <常量>}'}'{, '{'<常量>{, <常量>}'}'}'}'



<语句> ::= <循环语句> | <条件语句> | <赋值语句>; | <有返回值函数调用语句>; | <无返回值函数调用语句>; | <空>; | <返回语句>; | '{'<语句列>'}' | break;

<条件运算符> ::= && | ||

<条件> ::= <条件项>{<条件运算符><条件项>}

<条件项> ::= <表示式><关系运算符><表达式> | '('<条件>')'

<循环语句> ::= while '('<条件>')' <语句>

<条件语句> ::= if  '('<条件>')' <语句> [else <语句>]

<赋值语句> ::= <标识符> = <表达式> | <标识符> = '{'<表达式>{, <表达式>}'}' | <标识符> = '{''{'<表达式>{, <表达式>}'}'{, '{'<表达式>{, <表达式>}'}'}'}' | <标识符>'['<表达式>']' = <表达式> | <标识符>'['<表达式>']''['<表达式>']' = <表达式> 

<值参数表> ::= <表达式>{, <表达式>} | <空>

<有返回值函数调用语句> ::= <标识符>'('<值参数表>')'

<无返回值函数调用语句> ::= <标识符>'('<值参数表>')'

<返回语句> ::= return [<表达式>]



<声明头部> ::= <类型标识符> <标识符>

<参数表> ::= <类型标识符> <标识符>{, <类型标识符> <标识符>} | <空>

<复合语句> ::=[<变量说明>] <语句列>

<有返回值函数定义> ::= <声明头部>'('<参数表>')''{'<复合语句>'}'

<无返回值函数定义> ::= void <标识符> '('<参数表>')''{'<复合语句>'}'

<主函数> ::= void main '('')' '{'<复合语句>'}'

<语句列> ::= {<语句>}

<程序> ::= [<输入输出说明>] [<变量说明>] {<有返回值函数定义>|无返回值函数定义} <主函数>